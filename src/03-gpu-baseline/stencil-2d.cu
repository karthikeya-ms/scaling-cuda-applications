#include "../stencil-2d-util.h"

#include "../cuda-util.h"


inline cudaMemLocation deviceMemoryLocation(int deviceId) {
    return cudaMemLocation{cudaMemLocationTypeDevice, deviceId};
}

inline cudaMemLocation hostMemoryLocation() {
    return cudaMemLocation{cudaMemLocationTypeHost, 0};
}

__global__ void stencil2D(const double *__restrict__ u, double *__restrict__ uNew,
                          size_t globalNumCellsX, size_t globalNumCellsY) {
    const size_t i0 = blockIdx.x * blockDim.x + threadIdx.x;
    const size_t i1 = blockIdx.y * blockDim.y + threadIdx.y;

    if (i0 >= 1 && i0 < globalNumCellsX - 1 && i1 >= 1 && i1 < globalNumCellsY - 1) {
        uNew[i0 + i1 * globalNumCellsX] = u[i0 + i1 * globalNumCellsX]
            + alpha * (
                      u[(i0 - 1) +  i1      * globalNumCellsX]
                +     u[(i0 + 1) +  i1      * globalNumCellsX]
                +     u[ i0      + (i1 + 1) * globalNumCellsX]
                +     u[ i0      + (i1 - 1) * globalNumCellsX]
                - 4 * u[ i0      +  i1      * globalNumCellsX]
            );
    }
}

int main(int argc, char *argv[]) {
    // determine application parameters
    size_t globalNumCellsX, globalNumCellsY, numItWarmUp, numItTimed, printInterval;
    parseCLA_2d(argc, argv, globalNumCellsX, globalNumCellsY, numItWarmUp, numItTimed, printInterval);

    // allocation
    double *u;
    checkCudaError(cudaMallocManaged(&u, globalNumCellsX * globalNumCellsY * sizeof(double)));
    double *uNew;
    checkCudaError(cudaMallocManaged(&uNew, globalNumCellsX * globalNumCellsY * sizeof(double)));

    // init temperature fields including their boundaries
    initTemperature(u, uNew, globalNumCellsX, globalNumCellsY);

    // prefetch to GPU
    int deviceId = 0;
    checkCudaError(cudaGetDevice(&deviceId));
    checkCudaError(cudaMemPrefetchAsync(
        u, globalNumCellsX * globalNumCellsY * sizeof(double), deviceMemoryLocation(deviceId), 0));
    checkCudaError(cudaMemPrefetchAsync(
        uNew, globalNumCellsX * globalNumCellsY * sizeof(double), deviceMemoryLocation(deviceId), 0));

    // define execution configuration
    dim3 blockSize(16, 16);
    dim3 gridSize(ceilingDivide(globalNumCellsX - 1, blockSize.x),
                  ceilingDivide(globalNumCellsY - 1, blockSize.y));

    // print function
    auto print = [&](size_t it) {
        std::cout << "  Completed iteration " << it << std::endl;

        std::string idx = std::to_string(it);
        if (idx.size() < 6) idx = std::string(6 - idx.size(), '0') + idx;

        // Note: this could be optimized - see the course 'Fundamentals of Accelerated Computing with Modern CUDA C++'
        checkCudaError(cudaMemPrefetchAsync(
            u, globalNumCellsX * globalNumCellsY * sizeof(double), hostMemoryLocation(), 0));
        writeTemperatureNpy("../output/temperature_" + idx + ".npy", u, globalNumCellsX, globalNumCellsY);
        checkCudaError(cudaMemPrefetchAsync(
            u, globalNumCellsX * globalNumCellsY * sizeof(double), deviceMemoryLocation(deviceId), 0));
    };

    // work function
    auto work = [&](size_t it) {
        stencil2D<<<gridSize, blockSize>>>(u, uNew, globalNumCellsX, globalNumCellsY);
        checkCudaError(cudaGetLastError());
        std::swap(u, uNew);

        if (printInterval > 0 && 0 == (it % printInterval))
            print(it);
    };

    // warm-up
    for (size_t i = 0; i < numItWarmUp; ++i)
        work(i);

    // measurement
    checkCudaError(cudaDeviceSynchronize());
    nvtxRangePushA("work");
    auto start = std::chrono::steady_clock::now();

    for (size_t i = 0; i < numItTimed; ++i)
            work(i + numItWarmUp);    // account for warm-up iterations in the print interval computation

    checkCudaError(cudaDeviceSynchronize());
    auto end = std::chrono::steady_clock::now();
    nvtxRangePop();

    // print stats and diagnostic result
    printStats(end - start, numItTimed, globalNumCellsX * globalNumCellsY, sizeof(double) + sizeof(double), 7);

    checkCudaError(cudaMemPrefetchAsync(
        u, globalNumCellsX * globalNumCellsY * sizeof(double), hostMemoryLocation(), 0));
    auto totalTemperature = accumulateTemperature(u, globalNumCellsX, globalNumCellsY);
    std::cout << "  Total temperature is " << totalTemperature << std::endl;

    // clean up
    checkCudaError(cudaFree(u));
    checkCudaError(cudaFree(uNew));

    return 0;
}
